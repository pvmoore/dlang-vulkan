/* 
 * This is a modified version of Sascha Willems' ray tracing example.   
 * For original see https://github.com/SaschaWillems/Vulkan
 */
#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_debug_printf : enable

struct Sphere {
	vec3 center;
	float radius;
	vec4 color;
};
layout(binding = 2, set = 0) uniform UBO {
	mat4 viewInverse;
	mat4 projInverse;
	vec4 lightPos;
    uint option;
} ubo;
layout(binding = 3, set = 0) buffer Spheres { 
    Sphere s[]; 
} spheres;

// * Option 1 : Multiple primitives, single instance
// * Option 2 : Single primitive, multiple instances

float sphIntersect(const Sphere s, vec3 ro, vec3 rd) {
    vec3 oc = ro - s.center;
    float b = dot(oc, rd);
    float c = dot(oc, oc) - s.radius * s.radius;
    float h = b * b - c;
    if (h < 0.0) { 
        return -1.0;
    }
    h = sqrt(h);
    return -b - h;
}

void main() {
    const uint id = ubo.option == 1 ? gl_PrimitiveID : gl_InstanceID;
    Sphere sphere = spheres.s[id];

    float hit = sphIntersect(sphere, gl_WorldRayOriginEXT, gl_WorldRayDirectionEXT);

    if (hit > 0) {
        reportIntersectionEXT(hit, 0);
    } 
}
